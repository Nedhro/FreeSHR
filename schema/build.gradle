apply plugin: 'java'


repositories {
    mavenCentral()
    flatDir {
        dirs '../lib'
    }
}

configurations.all{
    resolutionStrategy {
        force 'com.google.guava:guava:18.0', 'org.apache.httpcomponents:httpclient:4.3.5'
    }
}
def mainClassName = "org.freeshr.cassandra.Migrations"

jar {
    baseName = 'shr-schema'
    version = '2.6-SNAPSHOT'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest{
        attributes('Main-Class': mainClassName)
    }
}


dependencies {
    compile libraries.mutagen
    compile libraries.mutagen_cassandra,libraries.commons_lang
    compile libraries.spring_data_cassandra,libraries.lz4,libraries.snappy
    compile(libraries.spring_core){
        force = true
    }
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task runMigration(type: JavaExec){
    Properties properties = new Properties();
    properties.load(new FileInputStream(file('../env/local.properties')))
    main = mainClassName
    jvmArgs = ['-DSHR_LOG_LEVEL=DEBUG']
    classpath = sourceSets.main.runtimeClasspath
    environment properties
}